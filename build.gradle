buildscript {
   repositories {
       maven {
         url 'http://artifactory.engr.wavemarket.com/repo'
       }
   }

   dependencies {
      classpath(group: 'com.colorcloud', name: 'gradle_ubuntu_packager', version:'1.2')
   }
}

// These properties are used in making debian package.
new File("package.json").withReader { reader ->
   // read the version from package.json and remove the build suffix
   project.version = new groovy.json.JsonSlurper().parse(reader).version.replaceFirst(~/-.+/, {""})
}
project.description = "Persistent socket server for push notifications"

apply plugin: 'ubuntu'

ubuntu {
   name         = "elephant"
   author       = "Location Labs"
   debDir       = "dpkg"
   email        = "info@colorcloud.com"
   homepage     = "http://colorcloud.com"
   releaseNotes = "Initial version of the elephant project."

   depends {
      on "nodejs"
      on "supervisor"
   }

   // elephant docs
   addFileToDeb(new File("dpkg/doc/README"),
                "/usr/share/doc/elephant")

   // elephant's package.json
   addFileToDeb(new File("package.json"),
                "/usr/lib/elephant")

   // elephant libraries
   addFilesToDeb(new File("lib").listFiles(),
                 "/usr/lib/elephant/lib")

   // elephant dependencies
   addFilesToDeb(new File("node_modules").listFiles(),
                 "/usr/lib/elephant/node_modules")

   // service configuration
   addFilesToDeb(new File("dpkg/conf/supervisor").listFiles(),
                 "/etc/supervisor/conf.d")
   addFilesToDeb(new File("dpkg/conf/logrotate.d").listFiles(),
                 "/etc/logrotate.d")

   // elephant's own configuration
   addFilesToDeb(new File("config").listFiles(),
                 "/usr/lib/elephant/config")

   dirs {
      dir "/var/log/elephant"
   }
}
